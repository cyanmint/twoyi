import java.text.SimpleDateFormat

/*
 * This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at https://mozilla.org/MPL/2.0/.
 */

plugins {
    id 'com.android.application'
}

android {
    namespace "io.twoyi"
    compileSdk 34
    buildToolsVersion "34.0.0"

    def buildSerial = new SimpleDateFormat("MMddHHmm").format(new Date())

    defaultConfig {
        applicationId "io.twoyi"
        minSdk 27
        //noinspection ExpiredTargetSdkVersion
        targetSdk 27
        versionCode 30505
        versionName "3.5.5-${buildSerial}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilters "arm64-v8a"
        }
        archivesBaseName = "twoyi_${versionName}"
    }

    buildFeatures {
        buildConfig true
    }

    buildTypes {
        release {
            minifyEnabled false
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    packaging {
        jniLibs {
            useLegacyPackaging true
        }
    }
}

task cargoBuild {
    doLast {
        def twoyiLib = new File(project.projectDir, "src/main/jniLibs/arm64-v8a/libtwoyi.so")
        if (twoyiLib.exists()) {
            println "libtwoyi.so already exists, skipping cargo build"
            return
        }
        exec {
            //切换到sh文件所在的目录，执行脚本
            workingDir new File(project.projectDir, "rs")
            //执行shell脚本 "," 传参
            commandLine "sh", "build_rs.sh", "--release"
        }
    }
}

tasks.whenTaskAdded { task ->
    if ((task.name == 'javaPreCompileDebug' || task.name == 'javaPreCompileRelease')) {
        task.dependsOn 'cargoBuild'
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'

    implementation 'com.github.tiann:FreeReflection:3.1.0'

    def libsuVersion = '5.2.2'
    implementation "com.github.topjohnwu.libsu:core:${libsuVersion}"

    // AboutPage
    implementation 'com.github.medyo:android-about-page:1.2.4'
    implementation 'de.psdev.licensesdialog:licensesdialog:2.2.0'
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'moe.feng:AlipayZeroSdk:1.1'

    implementation 'org.jdeferred:jdeferred-android-aar:1.2.6'
    implementation 'com.github.clans:fab:1.6.4'

    implementation ('com.github.bumptech.glide:glide:4.16.0')
    annotationProcessor 'com.github.bumptech.glide:compiler:4.16.0'
    annotationProcessor 'androidx.annotation:annotation:1.7.1'

    implementation 'org.apache.commons:commons-compress:1.26.1'
    implementation 'org.tukaani:xz:1.9'

    implementation 'com.github.hzy3774:AndroidP7zip:v1.7.2'
//    implementation "com.github.seven332.a7zip:extract:master-SNAPSHOT"
//    implementation 'com.github.omicronapps:7-Zip-JBinding-4Android:Release-16.02-2.02'
//    implementation 'com.victor:lib:1.0.4'
    implementation 'com.cleveroad:androidmanimation:0.9.1'

    def appCenterSdkVersion = '5.0.4'
    implementation "com.microsoft.appcenter:appcenter-analytics:${appCenterSdkVersion}"
    implementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"

//    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}